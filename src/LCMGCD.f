*DECK LCMGCD
      SUBROUTINE LCMGCD(IPLIST,NAMP,ILONG,HDATA)
*
*-----------------------------------------------------------------------
*
* COPY A CHARACTER VARIABLE FROM A TABLE INTO MEMORY.
*
* INPUT PARAMETERS:
*  IPLIST : ADDRESS OF THE TABLE.
*    NAMP : CHARACTER*12 NAME OF THE EXISTING BLOCK.
*   ILONG : DIMENSION OF THE CHARACTER VARIABLE.
*
* OUTPUT PARAMETER:
*   HDATA : CHARACTER VARIABLE. CHARACTER*(*) HDATA(ILONG)
*
*------------------------------------- AUTHOR: A. HEBERT ; 12/03/00 ----
*
      USE GANLIB
*----
*  SUBROUTINE ARGUMENTS
*----
      TYPE(C_PTR) IPLIST
      INTEGER ILONG
      CHARACTER*(*) NAMP,HDATA(ILONG)
*----
*  LOCAL VARIABLES
*----
      TYPE(C_PTR) JPLIST
      INTEGER, ALLOCATABLE, DIMENSION(:) :: IDATA
*
      ILE1=(LEN(HDATA(1))+3)/4
      CALL LCMLEN(IPLIST,NAMP,JLONG,ITYLCM)
      IF(ITYLCM.NE.10) CALL XABORT('LCMGCD: LIST EXPECTED.')
      IF(JLONG.GT.ILONG) CALL XABORT('LCMGCD: HDATA OVERFLOW.')
      JPLIST=LCMGID(IPLIST,NAMP)
      DO ISET=1,JLONG
         CALL LCMLEL(JPLIST,ISET,ILE2,ITYLCM)
         IF(ITYLCM.NE.3) CALL XABORT('LCMGCD: CHARACTER EXPECTED.')
         ALLOCATE(IDATA(ILE2))
         CALL LCMGDL(JPLIST,ISET,IDATA)
         HDATA(ISET)=' '
         WRITE(HDATA(ISET),'(100A4)') (IDATA(I),I=1,MIN(ILE1,ILE2))
         DEALLOCATE(IDATA)
      ENDDO
      DO ISET=JLONG+1,ILONG
         HDATA(ISET)=' '
      ENDDO
      RETURN
      END
